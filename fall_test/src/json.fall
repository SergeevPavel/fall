nodes {
  null bool number string
  lbrace rbrace
  lbrack rbrack
  comma colon
  object array primitive
  field
  file
}

tokenizer {
  lbrace     '{'
  rbrace     '}'
  lbrack     '['
  rbrack     ']'
  colon      ':'
  comma      ','
  null       'null'

  whitespace r"\s+"
  bool       r"true|false"
  string     r#""[^"]*""#
  number     r"\d+"
}

rule file {
  object | array
}

rule object {
  '{' <commit>
    object_body
  '}'
}

rule object_body { <rep {field <commit> <opt ','> } {string} {'}'}> }

rule field {
  string <commit> ':' value
}

rule array {
  '[' <commit>
    <rep {value <opt ','>} {'null'|number|string|bool|'{'|'['} {']'}>
  ']'
}

rule value { primitive | object | array }

rule primitive { 'null' | number | string | bool }
